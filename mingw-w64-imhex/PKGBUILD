# Maintainer: umarcor <unai.martinezcorral@ehu.eus>

_realname=imhex
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.6.1
pkgrel=1
pkgdesc="${_realname}: Hex Editor for Reverse Engineers, Programmers and people that value their eye sight when working at 3 AM (mingw-w64)"
arch=('any')
url="https://github.com/WerWolv/ImHex"
license=('GPLv2')
depends=("${MINGW_PACKAGE_PREFIX}-python")
makedepends=(
  "${MINGW_PACKAGE_PREFIX}-gcc"
  "${MINGW_PACKAGE_PREFIX}-cmake"
  "${MINGW_PACKAGE_PREFIX}-capstone"
  "${MINGW_PACKAGE_PREFIX}-dlfcn"
  "${MINGW_PACKAGE_PREFIX}-freetype"
  "${MINGW_PACKAGE_PREFIX}-glfw"
  "${MINGW_PACKAGE_PREFIX}-glm"
  "${MINGW_PACKAGE_PREFIX}-file"
  "${MINGW_PACKAGE_PREFIX}-nlohmann-json"
  "${MINGW_PACKAGE_PREFIX}-openssl"
  "${MINGW_PACKAGE_PREFIX}-polly"
)

source=("${_realname}::https://codeload.github.com/WerWolv/imhex/tar.gz/v${pkgver}")
sha256sums=('03cfe4ea9971dec548f9519be31793952b96da4e9204328f3ea059742b86639d')

build() {
  cd "${srcdir}/${_realname}-${pkgver}"
  mkdir build
  cd build
  MSYS2_ARG_CONV_EXCL='-DCMAKE_INSTALL_PREFIX' cmake \
    -G "MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX="${MINGW_PREFIX}" \
    -DCMAKE_BUILD_TYPE='Release' \
    ../
  make
}

package() {
  cd "${srcdir}/${_realname}-${pkgver}"/build
  make DESTDIR="${pkgdir}" install
  _bin="${pkgdir}${MINGW_PREFIX}"/bin/
  mkdir -p "${_bin}"
  mv "${pkgdir}${MINGW_PREFIX}/${_realname}".exe "${_bin}"

  install -D -m 644 ../LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}"/LICENSE
}
